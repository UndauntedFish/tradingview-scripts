// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© bjr117

//@version=5
indicator(title = "ATR with MA", shorttitle = "ATRMA", overlay = false)

//==============================================================================
// Function: Calculate a given type of moving average
//==============================================================================
get_ma_out(type, src, len, alma_offset, alma_sigma, kama_fastLength, kama_slowLength, vama_vol_len, vama_do_smth, vama_smth, mf_beta, mf_feedback, mf_z, eit_alpha) =>
    float baseline = 0.0
    
    if type == 'SMA  | Simple MA'
        baseline := ta.sma(src, len)

    else if type == 'EMA  | Exponential MA'
        baseline := ta.ema(src, len)

    else if type == 'DEMA | Double Exponential MA'
        e = ta.ema(src, len)
        baseline := 2 * e - ta.ema(e, len)

    else if type == 'TEMA | Triple Exponential MA'
        e = ta.ema(src, len)
        baseline := 3 * (e - ta.ema(e, len)) + ta.ema(ta.ema(e, len), len)

	else if type == 'TMA  | Triangular MA' // by everget
        baseline := ta.sma(ta.sma(src, math.ceil(len / 2)), math.floor(len / 2) + 1)

    else if type == 'WMA  | Weighted MA'
        baseline := ta.wma(src, len)

    else if type == 'VWMA | Volume-Weighted MA'
        baseline := ta.vwma(src, len)

    else if type == 'SMMA | Smoothed MA'
        w = ta.wma(src, len)
        baseline := na(w[1]) ? ta.sma(src, len) : (w[1] * (len - 1) + src) / len

    else if type == 'RMA  | Rolling MA'
        baseline := ta.rma(src, len)

    else if type == 'HMA  | Hull MA'
        baseline := ta.wma(2 * ta.wma(src, len / 2) - ta.wma(src, len), math.round(math.sqrt(len)))

    else if type == 'LSMA | Least Squares MA'
        baseline := ta.linreg(src, len, 0)

    else if type == 'Kijun'  //Kijun-sen
        kijun = math.avg(ta.lowest(len), ta.highest(len))
        baseline := kijun

    else if type == 'MD   | McGinley Dynamic'
        mg = 0.0
        mg := na(mg[1]) ? ta.ema(src, len) : mg[1] + (src - mg[1]) / (len * math.pow(src / mg[1], 4))
        baseline := mg

	else if type == 'JMA  | Jurik MA' // by everget
		DEMAe1 = ta.ema(src, len)
		DEMAe2 = ta.ema(DEMAe1, len)
		baseline := 2 * DEMAe1 - DEMAe2
		
    else if type == 'ALMA | Arnaud Legoux MA'
        baseline := ta.alma(src, len, alma_offset, alma_sigma)

    else if type == 'VAR  | Vector Autoregression MA'
        valpha = 2 / (len+1)
        vud1   = (src > src[1]) ? (src - src[1]) : 0
        vdd1   = (src < src[1]) ? (src[1] - src) : 0
        vUD    = math.sum(vud1, 9)
        vDD    = math.sum(vdd1, 9)
        vCMO   = nz( (vUD - vDD) / (vUD + vDD) )
        baseline := nz(valpha * math.abs(vCMO) * src) + (1 - valpha * math.abs(vCMO)) * nz(baseline[1])
        
    else if type == 'WWMA | Welles Wilder MA (aka Rolling MA)'
        baseline := nz(baseline[1]) + (src - nz(baseline[1])) / len
        
    else if type == 'ZLEMA | Zero-Lag Exponential MA' // by HPotter
        xLag     = (len) / 2
        xEMAData = (src + (src - src[xLag]))
        baseline := ta.ema(xEMAData, len)
        
    else if type == 'AHMA | Ahrens Moving Average' // by everget
        baseline := nz(baseline[1]) + (src - (nz(baseline[1]) + nz(baseline[len])) / 2) / len 
        
    else if type == 'EVWMA | Elastic Volume Weighted MA'
        volumeSum = math.sum(volume, len)
        baseline := ( (volumeSum - volume) * nz(baseline[1]) + volume * src ) / volumeSum
    
    else if type == 'SWMA | Sine Weighted MA' // by everget
        sum = 0.0
        weightSum = 0.0
        for i = 0 to len - 1
            weight = math.sin(i * math.pi / (len + 1))
            sum := sum + nz(src[i]) * weight
            weightSum := weightSum + weight
        baseline := sum / weightSum
        
    else if type == 'LMA  | Leo MA'
        baseline := 2 * ta.wma(src, len) - ta.sma(src, len)
        
    else if type == 'VIDYA | Variable Index Dynamic Average' // by KivancOzbilgic
        mom = ta.change(src)
        upSum = math.sum(math.max(mom, 0), len)
        downSum = math.sum(-math.min(mom, 0), len)
        out = (upSum - downSum) / (upSum + downSum)

        cmo = math.abs(out)
        
        alpha = 2 / (len + 1)
        
        baseline := src * alpha * cmo + nz(baseline[1]) * (1 - alpha * cmo)
    
    else if type == 'FRAMA | Fractal Adaptive MA'
        length2 = math.floor(len / 2)
        hh2 = ta.highest(length2)
        ll2 = ta.lowest(length2)
        
        N1 = (hh2 - ll2) / length2
        
        N2 = (hh2[length2] - ll2[length2]) / length2
        
        N3 = (ta.highest(len) - ta.lowest(len)) / len
        
        D = (math.log(N1 + N2) - math.log(N3)) / math.log(2)
        
        factor = math.exp(-4.6 * (D - 1))
        
        baseline := factor * src + (1 - factor) * nz(baseline[1])
    
    else if type == 'VMA  | Variable MA' // by LazyBear
        k = 1.0/len
        pdm = math.max((src - src[1]), 0)
        mdm = math.max((src[1] - src), 0)
        
        pdmS  = float(0.0)
        mdmS  = float(0.0)
        pdmS := ((1 - k)*nz(pdmS[1]) + k*pdm)
        mdmS := ((1 - k)*nz(mdmS[1]) + k*mdm)
        
        s = pdmS + mdmS
        pdi = pdmS/s
        mdi = mdmS/s
        
        pdiS  = float(0.0)
        mdiS  = float(0.0)
        pdiS := ((1 - k)*nz(pdiS[1]) + k*pdi)
        mdiS := ((1 - k)*nz(mdiS[1]) + k*mdi)
        
        d = math.abs(pdiS - mdiS)
        s1 = pdiS + mdiS
        
        iS  = float(0.0)
        iS := ((1 - k)*nz(iS[1]) + k*d/s1)
        
        hhv = ta.highest(iS, len) 
        llv = ta.lowest(iS, len) 
        d1 = hhv - llv
        vI = (iS - llv)/d1
        baseline := (1 - k*vI)*nz(baseline[1]) + k*vI*src
    
    else if type == 'GMMA | Geometric Mean MA'
        lmean = math.log(src)
        smean = math.sum(lmean, len)
        baseline := math.exp(smean / len)
        
    else if type == 'CMA  | Corrective MA' // by everget
        sma = ta.sma(src, len)
        baseline := sma
        v1 = ta.variance(src, len)
        v2 = math.pow(nz(baseline[1], baseline) - baseline, 2)
        v3 = v1 == 0 or v2 == 0 ? 1 : v2 / (v1 + v2)
        
        var tolerance = math.pow(10, -5)
        float err = 1
        
        // Gain Factor
        float kPrev = 1
        float k = 1
        
        for i = 0 to 5000 by 1
            if err > tolerance
                k := v3 * kPrev * (2 - kPrev)
                err := kPrev - k
                kPrev := k
                kPrev
                
        baseline := nz(baseline[1], src) + k * (sma - nz(baseline[1], src))
        
    else if type == 'MM   | Moving Median' // by everget
        baseline := ta.percentile_nearest_rank(src, len, 50)
        
    else if type == 'QMA  | Quick MA' // by everget
        peak = len / 3
        num = 0.0
        denom = 0.0
         
        for i = 1 to len + 1
            mult = 0.0
            if i <= peak
                mult := i / peak
            else
                mult := (len + 1 - i) / (len + 1 - peak)
            num := num + src[i - 1] * mult
            denom := denom + mult
         
        baseline := (denom != 0.0) ? (num / denom) : src
        
    else if type == 'KAMA | Kaufman Adaptive MA' // by everget
        mom = math.abs(ta.change(src, len))
        volatility = math.sum(math.abs(ta.change(src)), len)
        
        // Efficiency Ratio
        er = volatility != 0 ? mom / volatility : 0
        
        fastAlpha = 2 / (kama_fastLength + 1)
        slowAlpha = 2 / (kama_slowLength + 1)
        
        alpha = math.pow(er * (fastAlpha - slowAlpha) + slowAlpha, 2)
        
        baseline := alpha * src + (1 - alpha) * nz(baseline[1], src)
    
    else if type == 'VAMA | Volatility Adjusted MA' // by Joris Duyck (JD)
        mid = ta.ema(src, len)
        dev = src - mid
        
        vol_up   = ta.highest(dev, vama_vol_len)
        vol_down = ta.lowest(dev, vama_vol_len)
        
        vama = mid + math.avg(vol_up, vol_down)
        baseline := vama_do_smth ? ta.wma(vama, vama_smth) : vama
    
    else if type == 'Modular Filter' // by alexgrover
        //----
        b = 0.0, c = 0.0, os = 0.0, ts = 0.0
        //----
        alpha = 2/(len+1)
        a = mf_feedback ? mf_z*src + (1-mf_z)*nz(baseline[1],src) : src
        //----
        b := a > alpha*a+(1-alpha)*nz(b[1],a) ? a : alpha*a+(1-alpha)*nz(b[1],a)
        c := a < alpha*a+(1-alpha)*nz(c[1],a) ? a : alpha*a+(1-alpha)*nz(c[1],a)
        os := a == b ? 1 : a == c ? 0 : os[1]
        //----
        upper = mf_beta*b+(1-mf_beta)*c
        lower = mf_beta*c+(1-mf_beta)*b 
        baseline := os*upper+(1-os)*lower
    
    else if type == 'EIT  | Ehlers Instantaneous Trendline' // by Franklin Moormann (cheatcountry)
        baseline := bar_index < 7 ? (src + (2 * nz(src[1])) + nz(src[2])) / 4 : ((eit_alpha - (math.pow(eit_alpha, 2) / 4)) * src) + (0.5 * math.pow(eit_alpha, 2) * nz(src[1])) - 
         ((eit_alpha - (0.75 * math.pow(eit_alpha, 2))) * nz(src[2])) + (2 * (1 - eit_alpha) * nz(baseline[1])) - (math.pow(1 - eit_alpha, 2) * nz(baseline[2]))
    
    else if type == 'ESD  | Ehlers Simple Decycler' // by everget
        // High-pass Filter
        alphaArg = 2 * math.pi / (len * math.sqrt(2))
        
        alpha = 0.0
        alpha := math.cos(alphaArg) != 0
             ? (math.cos(alphaArg) + math.sin(alphaArg) - 1) / math.cos(alphaArg)
             : nz(alpha[1])
        
        hp = 0.0
        hp := math.pow(1 - (alpha / 2), 2) * (src - 2 * nz(src[1]) + nz(src[2])) + 2 * (1 - alpha) * nz(hp[1]) - math.pow(1 - alpha, 2) * nz(hp[2])
    
        baseline := src - hp
    baseline
//==============================================================================


//==============================================================================
// Inputs
//==============================================================================
// Main inputs for ATR
atr_length    = input.int(14, title = "ATR Length", minval = 1, group = 'ATR Settings', inline = 'ATR Settings')
atr_smth_type = input.string('RMA  | Rolling MA', 'ATR Smoothing', options = [ 'EMA  | Exponential MA',                  'SMA  | Simple MA', 
                                                                               'WMA  | Weighted MA',                     'DEMA | Double Exponential MA', 
                                                                               'TEMA | Triple Exponential MA',           'TMA  | Triangular MA', 
                                                                               'VWMA | Volume-Weighted MA',              'SMMA | Smoothed MA', 
                                                                               'HMA  | Hull MA',                         'LSMA | Least Squares MA', 
                                                                               'Kijun',                                  'MD   | McGinley Dynamic', 
                                                                               'RMA  | Rolling MA',                      'JMA  | Jurik MA', 
                                                                               'ALMA | Arnaud Legoux MA',                'VAR  | Vector Autoregression MA', 
                                                                               'WWMA | Welles Wilder MA',                'ZLEMA | Zero-Lag Exponential MA',
                                                                               'AHMA | Ahrens Moving Average',           'EVWMA | Elastic Volume Weighted MA',
                                                                               'SWMA | Sine Weighted MA',                'LMA  | Leo MA',
                                                                               'VIDYA | Variable Index Dynamic Average', 'FRAMA | Fractal Adaptive MA',
                                                                               'VMA  | Variable MA',                     'GMMA | Geometric Mean MA',
                                                                               'CMA  | Corrective MA',                   'MM   | Moving Median',
                                                                               'QMA  | Quick MA',                        'KAMA | Kaufman Adaptive MA',
                                                                               'VAMA | Volatility Adjusted MA',          'Modular Filter',
                                                                               'EIT  | Ehlers Instantaneous Trendline',  'ESD  | Ehlers Simple Decycler'
                                                                               ], group = 'ATR Settings', inline = 'ATR Settings')

// Main inputs for Moving Average
atr_ma_length = input.int(20, title = "MA Length", minval = 2, group = 'ATR MA Settings', inline = 'ATR Settings')
atr_ma_type   = input.string('SMA  | Simple MA', 'MA Type', options = [  'EMA  | Exponential MA',                  'SMA  | Simple MA', 
                                                                         'WMA  | Weighted MA',                     'DEMA | Double Exponential MA', 
                                                                         'TEMA | Triple Exponential MA',           'TMA  | Triangular MA', 
                                                                         'VWMA | Volume-Weighted MA',              'SMMA | Smoothed MA', 
                                                                         'HMA  | Hull MA',                         'LSMA | Least Squares MA', 
                                                                         'Kijun',                                  'MD   | McGinley Dynamic', 
                                                                         'RMA  | Rolling MA',                      'JMA  | Jurik MA', 
                                                                         'ALMA | Arnaud Legoux MA',                'VAR  | Vector Autoregression MA', 
                                                                         'WWMA | Welles Wilder MA',                'ZLEMA | Zero-Lag Exponential MA',
                                                                         'AHMA | Ahrens Moving Average',           'EVWMA | Elastic Volume Weighted MA',
                                                                         'SWMA | Sine Weighted MA',                'LMA  | Leo MA',
                                                                         'VIDYA | Variable Index Dynamic Average', 'FRAMA | Fractal Adaptive MA',
                                                                         'VMA  | Variable MA',                     'GMMA | Geometric Mean MA',
                                                                         'CMA  | Corrective MA',                   'MM   | Moving Median',
                                                                         'QMA  | Quick MA',                        'KAMA | Kaufman Adaptive MA',
                                                                         'VAMA | Volatility Adjusted MA',          'Modular Filter',
                                                                         'EIT  | Ehlers Instantaneous Trendline',  'ESD  | Ehlers Simple Decycler'
                                                                         ], group = 'ATR MA Settings', inline = 'ATR Settings')

// Specific smoothing settings for ATR
atr_smth_alma_offset = input.float(title = "Offset (ATR)",    defval = 0.85, step = 0.05, group = 'ALMA Settings', inline = 'ALMA Offset')
atr_smth_alma_sigma  = input.int(title   = 'Sigma (ATR)', defval = 6,                     group = 'ALMA Settings', inline = 'ALMA Sigma')

atr_smth_kama_fastLength = input(title = 'Fast EMA Length (ATR)', defval=2,  group = 'KAMA Settings', inline = 'KAMA Fast EMA Length')
atr_smth_kama_slowLength = input(title = 'Slow EMA Length (ATR)', defval=30, group = 'KAMA Settings', inline = 'KAMA Slow EMA Length')

atr_smth_vama_vol_len = input.int(title  = 'Volatality Length (ATR)', defval = 51,            group = 'VAMA Settings', inline = 'VAMA Volatility Length')
atr_smth_vama_do_smth = input.bool(title = 'Do Smoothing? (ATR)',     defval = false,         group = 'VAMA Settings', inline = 'VAMA Do Smoothing?')
atr_smth_vama_smth    = input.int(title  = 'Smoothing length (ATR)',  defval = 5, minval = 1, group = 'VAMA Settings', inline = 'VAMA Smoothing length')

atr_smth_mf_beta     = input.float(title = 'Beta (ATR)',               defval = 0.8, step = 0.1, minval = 0, maxval=1,   group = 'Modular Filter Settings', inline = 'MF Beta')
atr_smth_mf_feedback = input.bool(title  = 'Feedback? (ATR)',          defval = false,                                   group = 'Modular Filter Settings', inline = 'MF Feedback?')
atr_smth_mf_z        = input.float(title = 'Feedback Weighting (ATR)', defval = 0.5, step = 0.1, minval = 0, maxval = 1, group = 'Modular Filter Settings', inline = 'MF Feedback Weighting')

atr_smth_eit_alpha = input.float(title = 'Alpha (ATR)', defval = 0.07, step = 0.01, minval = 0.0, group = 'Ehlers Instantaneous Trendline Settings', inline = 'EIT Alpha')


// Specific smoothing settings for Moving Average
atr_ma_alma_offset = input.float(title = "Offset (MA)",    defval = 0.85, step = 0.05, group = 'ALMA Settings', inline = 'ALMA Offset')
atr_ma_alma_sigma  = input.int(title   = 'Sigma (MA)', defval = 6,                     group = 'ALMA Settings', inline = 'ALMA Sigma')

atr_ma_kama_fastLength = input(title = 'Fast EMA Length (MA)', defval=2,  group = 'KAMA Settings', inline = 'KAMA Fast EMA Length')
atr_ma_kama_slowLength = input(title = 'Slow EMA Length (MA)', defval=30, group = 'KAMA Settings', inline = 'KAMA Slow EMA Length')

atr_ma_vama_vol_len = input.int(title  = 'Volatality Length (MA)', defval = 51,            group = 'VAMA Settings', inline = 'VAMA Volatility Length')
atr_ma_vama_do_smth = input.bool(title = 'Do Smoothing? (MA)',     defval = false,         group = 'VAMA Settings', inline = 'VAMA Do Smoothing?')
atr_ma_vama_smth    = input.int(title  = 'Smoothing length (MA)',  defval = 5, minval = 1, group = 'VAMA Settings', inline = 'VAMA Smoothing length')

atr_ma_mf_beta     = input.float(title = 'Beta (MA)',               defval = 0.8, step = 0.1, minval = 0, maxval=1,   group = 'Modular Filter Settings', inline = 'MF Beta')
atr_ma_mf_feedback = input.bool(title  = 'Feedback? (MA)',          defval = false,                                   group = 'Modular Filter Settings', inline = 'MF Feedback?')
atr_ma_mf_z        = input.float(title = 'Feedback Weighting (MA)', defval = 0.5, step = 0.1, minval = 0, maxval = 1, group = 'Modular Filter Settings', inline = 'MF Feedback Weighting')

atr_ma_eit_alpha = input.float(title = 'Alpha (MA)', defval = 0.07, step = 0.01, minval = 0.0, group = 'Ehlers Instantaneous Trendline Settings', inline = 'EIT Alpha')
//==============================================================================


//==============================================================================
// Calculate ATR and ATR MA
//==============================================================================
atr    = get_ma_out(atr_smth_type, ta.tr(true), atr_length,    atr_smth_alma_offset, atr_smth_alma_sigma, atr_smth_kama_fastLength, atr_smth_kama_slowLength, atr_smth_vama_vol_len, atr_smth_vama_do_smth, atr_smth_vama_smth, atr_smth_mf_beta, atr_smth_mf_feedback, atr_smth_mf_z, atr_smth_eit_alpha)
atr_ma = get_ma_out(atr_ma_type,   atr,         atr_ma_length, atr_ma_alma_offset,   atr_ma_alma_sigma,   atr_ma_kama_fastLength,   atr_ma_kama_slowLength,   atr_ma_vama_vol_len,   atr_ma_vama_do_smth,   atr_ma_vama_smth,   atr_ma_mf_beta,   atr_ma_mf_feedback,   atr_ma_mf_z,   atr_ma_eit_alpha)
//==============================================================================


//==============================================================================
// Plot ATR and ATR MA
//==============================================================================
atr_plot    = plot(atr,    title = "ATR",    color = color.new(#000000, 0),  linewidth = 2)
atr_ma_plot = plot(atr_ma, title = "ATR MA", color = color.new(#000000, 70))

fillColor = atr > atr_ma ? color.blue : color.red
fill(atr_plot, atr_ma_plot, title = "ATR Fill", color = color.new(fillColor, 70))
//==============================================================================